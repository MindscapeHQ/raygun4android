apply plugin: 'com.android.library'

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 34
        versionCode = Long.valueOf(VERSION_CODE)
        versionName = VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
        }
        release {
            debuggable false
            minifyEnabled false
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'LogNotTimber', 'StringFormatInTimber', 'ThrowableNotAtBeginning', 'BinaryOperationInTimber', 'TimberArgCount', 'TimberArgTypes', 'TimberTagLength'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.raygun.raygun4android'

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                if (variant.name == 'debug') {
                    outputFileName = "raygun4android-debug.aar"
                } else {
                    outputFileName = "raygun4android.aar"
                }
            }
        }
    }

    libraryVariants.all { variant ->
        if (variant.name == 'release') {
            task androidJavadocs(type: Javadoc) {
                source = android.sourceSets.main.java.srcDirs
                source += android.sourceSets.release.java.srcDirs
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
                classpath += variant.javaCompiler.classpath
                classpath += files("build/generated/source/buildConfig/release")

                options.windowTitle = "Raygun4Android"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:support-compat:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.squareup.okhttp3:okhttp:3.12.2'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.12.2'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

// Commented code to push to lib repos as that code will need to change
// apply from: 'gradle-mvn-push.gradle'